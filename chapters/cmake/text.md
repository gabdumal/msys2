# Usando o CMake

O CMake √© uma ferramenta de c√≥digo aberto que permite a automa√ß√£o do processo de compila√ß√£o de projetos em C e C++.
Ele gera arquivos de configura√ß√£o para diferentes ambientes de desenvolvimento, como Visual Studio, Makefile, Ninja, entre outros.

Para n√≥s, o CMake ser√° √∫til para compilar projetos mais complexos, que envolvem v√°rios arquivos em diferentes diret√≥rios e depend√™ncias externas.

## Instalando o CMake

Para instalar o CMake, execute o comando abaixo no terminal do MSYS2:

```bash
pacman -S mingw-w64-clang-x86_64-cmake
```

Aceite a instala√ß√£o e confirme a instala√ß√£o dos pacotes necess√°rios.
Voc√™ pode verificar se o CMake foi instalado corretamente executando o comando `cmake --version`.

## Extens√µes

Para trabalhar com o CMake no Visual Studio Code, √© recomendado instalar algumas extens√µes que s√£o inclusas no pacote `C/C++ Extension Pack`.

Lembre-se de selecionar o perfil `MSYS2 Clang64` no Visual Studio Code antes de instalar a extens√£o.
Pesquise pela extens√£o [`C/C++ Extension Pack`](https://marketplace.visualstudio.com/items?itemName=ms-vscode.cpptools-extension-pack) e a instale.

Esse pacote inclui as seguintes extens√µes:

- [C/C++](https://marketplace.visualstudio.com/items?itemName=ms-vscode.cpptools): fornece suporte para desenvolvimento em C e C++.
- [C/C++ Themes](https://marketplace.visualstudio.com/items?itemName=ms-vscode.cpptools-themes): fornece temas de cores para o Visual Studio Code.
- [CMake](https://marketplace.visualstudio.com/items?itemName=twxs.cmake): fornece suporte para desenvolvimento com CMake.
- [CMake Tools](https://marketplace.visualstudio.com/items?itemName=ms-vscode.cmake-tools): fornece ferramentas para trabalhar com projetos CMake.

Caso seja disparado um pop-up pedindo para selecionar um kit de compila√ß√£o, selecione o kit `Clang`.

![Pop-up do Visual Studio Code, em que √© solicitado escolher um kit de compila√ß√£o.](selecting_compiling_kit.png)

## Criando o projeto

Crie um novo diret√≥rio chamado `oriented_graph` dentro de `~/dev`.
Acesse-o e abra o Visual Studio Code com o comando `code .`.

Dentro do diret√≥rio `oriented_graph`, crie um arquivo `main.cpp` e tr√™s pastas: `node`, `edge` e `graph`.
Cada pasta deve conter um arquivo `.cpp` e um arquivo `.h` com o mesmo nome da pasta.
**Ignore** os arquivos `CMakeLists.txt` por enquanto.

Voc√™ pode encontrar o c√≥digo completo deste projeto na pasta [`/code/oriented_graph`](/code/graph) deste reposit√≥rio.
Copie o c√≥digo de cada arquivo para o seu projeto.

Assim como antes, voc√™ pode compilar esse projeto diretamente pelo `clang++` ou utilizando os arquivos `tasks.json` e `launch.json` do Visual Studio Code.

O comando de compila√ß√£o √© o seguinte:

```bash
clang++ main.cpp node/node.cpp edge/edge.cpp graph/graph.cpp -I node -I edge -I graph -o oriented_graph
```

Ao executar o programa, voc√™ ver√° a seguinte sa√≠da: `1 3 4 5 2`.
Ela representa a ordem de visita√ß√£o dos n√≥s do grafo em uma busca em profundidade iniciando pelo n√≥ 1.

## Configurando o CMake

Uau, esses comandos est√£o ficando grandes!
A situa√ß√£o fica ainda mais complicada conforme se aumenta o n√∫mero de arquivos e depend√™ncias do projeto.
Felizmente, podemos simplificar esse gerenciamento com o CMake.

### Criando o arquivo `CMakeLists.txt`

Crie um arquivo chamado `CMakeLists.txt` na raiz da pasta `oriented_graph`.
Atente-se √† letras mai√∫sculas e min√∫sculas no nome do arquivo, ele precisa estar escrito exatamente como acima.

Este arquivo √© respons√°vel por definir as configura√ß√µes do projeto e as depend√™ncias necess√°rias para a compila√ß√£o.
Seu c√≥digo completo est√° dispon√≠vel em [`/code/oriented_graph/CMakeLists.txt`](/code/graph/CMakeLists.txt).

### Vers√µes

Vamos passar trecho por trecho do arquivo.

Primeiramente, √© necess√°rio definir a vers√£o m√≠nima do CMake que ser√° utilizada.
Na √©poca de escrita deste guia, a vers√£o 3.30.3 era a mais recente dispon√≠vel para o MSYS2.

N√≥s podemos definir um intervalo de vers√µes suportadas, para garantir que o projeto seja compilado corretamente em diferentes ambientes.
Isso √© feito com o comando `cmake_minimum_required`.

Em seguida, conferimos a restri√ß√£o de vers√£o do CMake e ajustamos a pol√≠tica de compatibilidade com vers√µes anteriores, caso necess√°rio.

```cmake
# Version
cmake_minimum_required(VERSION 3.12...3.30)

if (${CMAKE_VERSION} VERSION_LESS 3.12)
    cmake_policy(VERSION ${CMAKE_MAJOR_VERSION}.${CMAKE_MINOR_VERSION})
endif ()
```

### Defini√ß√µes do projeto

O pr√≥ximo passo √© definir o nome do projeto e a linguagem de programa√ß√£o utilizada.
Este nome ser√° utilizado como identificador para associar a ele depend√™ncias, execut√°veis e outras configura√ß√µes.
Para o CMake, a linguagem C++ √© identificada pela sigla `CXX`.

```cmake
# Project
project(oriented_graph
        LANGUAGES CXX
)
```

### Configura√ß√µes do ambiente

Definimos ent√£o algumas vari√°veis que definem o ambiente de compila√ß√£o.
Aqui, estamos definindo o padr√£o C++23, que √© o mais recente dispon√≠vel no momento da escrita deste guia.
Essa vers√£o da linguagem traz diversas melhorias e novas funcionalidades, aprimorando a seguran√ßa e a efici√™ncia do c√≥digo.

O segundo comando desativa extens√µes de linguagem que n√£o s√£o padr√£o, garantindo que o c√≥digo seja compat√≠vel com diferentes compiladores.
O terceiro, por sua vez, define que a vers√£o C++23 √© obrigat√≥ria.
Dessa forma, o CMake ir√° gerar um erro caso a vers√£o do compilador n√£o seja compat√≠vel com o padr√£o.

```cmake
# Environment
set(CMAKE_CXX_STANDARD 23)
set(CMAKE_CXX_EXTENSIONS OFF)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
```

### Flags de compila√ß√£o

Adicionamos algumas flags de compila√ß√£o para garantir que o c√≥digo seja compilado de forma otimizada e segura.
`-Wall` e `-Wextra` habilitam avisos de compila√ß√£o que podem indicar problemas no c√≥digo.
`-pedantic` habilita avisos adicionais, que podem ser √∫teis para garantir a portabilidade do c√≥digo.

```cmake
# Compilation flags
set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Wall -Wextra -pedantic")
```

### Diret√≥rios

Agora, devemos definir todos os diret√≥rios que incluem c√≥digo fonte e arquivos de cabe√ßalho do projeto.
Esses diret√≥rios s√£o adicionados ao projeto com o comando `add_subdirectory`, que busca por arquivos `CMakeLists.txt` dentro de cada pasta e os executa.

Dessa forma, √© poss√≠vel dividir o projeto em diferentes m√≥dulos e gerenciar suas depend√™ncias de forma independente.

Os comandos abaixo incluem os tr√™s diret√≥rios `node`, `edge` e `graph` que criamos.

```cmake
# Directories
add_subdirectory(node)
add_subdirectory(edge)
add_subdirectory(graph)
```

Atente-se que, para cada diret√≥rio, √© necess√°rio criar um arquivo `CMakeLists.txt` que defina as configura√ß√µes espec√≠ficas daquele m√≥dulo.

Al√©m disso, se uma dessas pastas definir outra pasta dentro de si, √© necess√°rio identificar essa segunda com o comando `add_subdirectory` no arquivo `CMakeLists.txt` da pasta pai.

Ou seja, se a pasta `node` tivesse uma subpasta `data_type`, por exemplo, seria necess√°rio adicionar `add_subdirectory(data_type)` no arquivo `node/CMakeLists.txt`.

### Execut√°vel

Ent√£o, definimos o execut√°vel que ser√° gerado a partir do projeto.
O comando `add_executable` recebe o nome do execut√°vel e a lista de arquivos `.cpp` que comp√µem aquele m√≥dulo.

No nosso caso, chamaremos o execut√°vel de `oriented_graph` e ele ser√° composto apenas pelo arquivo `main.cpp`.

```cmake
# Executable
add_executable(oriented_graph main.cpp)
```

### Incluindo o m√≥dulo

Para cada m√≥dulo, √© necess√°rio incluir os arquivos de cabe√ßalho e de c√≥digo fonte no projeto.
Fazemos isso com o comando `target_include_directories`.
Ele recebe o nome do execut√°vel e uma vari√°vel que representa a pasta do arquivo `CMakeLists.txt` atual.

```cmake
# Include module
target_include_directories(oriented_graph
    PUBLIC
        ${CMAKE_CURRENT_LIST_DIR}
)
```

## Configurando os diret√≥rios

Crie um arquivo `CMakeLists.txt` dentro de cada pasta `node`, `edge` e `graph`.
Esses arquivos s√£o respons√°veis por definir as configura√ß√µes espec√≠ficas de cada m√≥dulo.

Para cada pasta, vamos criar uma nova biblioteca est√°tica que cont√©m o c√≥digo fonte daquele m√≥dulo.
Uma biblioteca est√°tica √© um arquivo que cont√©m fun√ß√µes e vari√°veis que podem ser utilizadas por outros arquivos do projeto.

### M√≥dulo `node`

Dentro do arquivo `node/CMakeLists.txt`, definimos o nome da biblioteca est√°tica como `node`.

```cmake
# Project
add_library(node)
```

Precisamos ent√£o listar os arquivos de c√≥digo fonte e de cabe√ßalho que comp√µem o m√≥dulo.
Os de cabe√ßalho devem ser listados como p√∫blicos, uma vez que suas classes, fun√ß√µes e vari√°veis ser√£o utilizadas por outros m√≥dulos.
J√° os de c√≥digo fonte s√£o listados como privados, pois apenas implementam internamente as defini√ß√µes do m√≥dulo.

```cmake
# Sources
target_sources(node
    PUBLIC
        node.hpp
    PRIVATE
        node.cpp
)
```

√â necess√°rio tamb√©m incluir o diret√≥rio atual ao projeto, para que o CMake saiba onde buscar por arquivos de cabe√ßalho e de c√≥digo fonte.

```cmake
# Include module
target_include_directories(node
    INTERFACE
        ${CMAKE_CURRENT_LIST_DIR}
)
```

### M√≥dulo `edge`

O m√≥dulo `edge` √© definido de forma semelhante ao m√≥dulo `node`.
Dentro do arquivo `edge/CMakeLists.txt`, definimos o nome da biblioteca est√°tica como `edge`, listamos os arquivos de c√≥digo fonte e de cabe√ßalho e inclu√≠mos o diret√≥rio atual ao projeto.

```cmake
# Project
add_library(edge)

# Sources
target_sources(edge
    PUBLIC
        edge.hpp
    PRIVATE
        edge.cpp
)

# Include module
target_include_directories(edge
    INTERFACE
        ${CMAKE_CURRENT_LIST_DIR}
)
```

Mas h√° um detalhe: o m√≥dulo `edge` depende de outro m√≥dulo definido no projeto.
Veja o arquivo [`/code/oriented_graph/edge/edge.hpp`](/code/graph/edge/edge.hpp).

```cpp
...
#include "../node/node.hpp"

class Edge
{
private:
    unsigned long id;
    Node *source;
    Node *target;
    int weight;
...
```

A linha `#include "../node/node.hpp"` indica que o m√≥dulo `edge` depende da biblioteca est√°tica que chamamos de `node`.
Isto √© porque √© nela que se define a classe `Node`.

Esse comando √© interpretado da seguinte forma:

- A partir do arquivo `edge.hpp`, suba um n√≠vel no diret√≥rio (volte para a pasta `/oriented_graph`)
- Entre na pasta `node`
- Inclua o arquivo `node.hpp`

Apesar de termos especificado essa linha de inclus√£o, o CMake n√£o sabe que o m√≥dulo `edge` depende do m√≥dulo `node`.
Precisamos declarar essa depend√™ncia explicitamente.

Fazemos isso ao adicionar um √∫ltimo comando ao arquivo `edge/CMakeLists.txt`.
O comando `target_link_libraries` recebe o nome do m√≥dulo atual e, em seguida, o nome de quais bibliotecas ele depende.

```cmake
# Linking libraries
target_link_libraries(edge
    PRIVATE
        node
)
```

Podemos inclusive definir qual √© o grau de visibilidade da depend√™ncia.

- `PRIVATE` indica que a depend√™ncia √© utilizada apenas internamente pelo m√≥dulo `edge`.
- `PUBLIC` indica que a depend√™ncia ser√° usada pelo m√≥dulo e re-exportada para outros m√≥dulos que dependem de `edge`.
- `INTERFACE` indica que a depend√™ncia ser√° apenas re-exportada para outros m√≥dulos que dependem de `edge`, mas n√£o utilizada internamente.

### M√≥dulo `graph`

O m√≥dulo `graph` √© definido de forma semelhante aos m√≥dulos `node` e `edge`.

Dentro do arquivo `graph/CMakeLists.txt`, definimos o nome da biblioteca est√°tica como `graph`, listamos os arquivos de c√≥digo fonte e de cabe√ßalho e inclu√≠mos o diret√≥rio atual ao projeto.

```cmake
# Project
add_library(graph)

# Sources
target_sources(graph
    PUBLIC
        graph.hpp
    PRIVATE
        graph.cpp
)

# Include module
target_include_directories(graph
    INTERFACE
        ${CMAKE_CURRENT_LIST_DIR}
)
```

O m√≥dulo `graph` depende dos m√≥dulos `node` e `edge`, como indicado no arquivo [`/code/oriented_graph/graph/graph.hpp`](/code/graph/graph/graph.hpp).

```cpp
...
#include "../node/node.hpp"
#include "../edge/edge.hpp"

using namespace std;

class Graph
{
private:
    unsigned long lastNodeId;
    vector<Node *> nodes;
    unsigned long lastEdgeId;
    vector<Edge *> edges;
...
```

Dessa forma, precisamos adicionar a depend√™ncia dos m√≥dulos `node` e `edge` ao m√≥dulo `graph`.

```cmake
# Linking libraries
target_link_libraries(graph
    PRIVATE
        node
        edge
)
```

### Ligando os m√≥dulos ao execut√°vel

Definimos cada um dos m√≥dulos do projeto, mas ainda n√£o dissemos ao CMake que eles devem ser usados para gerar nosso execut√°vel.
A forma de fazer isso √© a mesma que usamos para definir as depend√™ncias entre os m√≥dulos.

Veja que no arquivo [`/code/oriented_graph/main.cpp`](/code/graph/main.cpp), inclu√≠mos o arquivo de cabe√ßalho do m√≥dulo `graph`.

```cpp
...
#include "graph/graph.hpp"

using namespace std;

int main()
{
    Graph graph;
...
```

Precisamos, ent√£o, adicionar essa depend√™ncia ao arquivo `CMakeLists.txt` da raiz do projeto.
Abra o arquivo `oriented_graph/CMakeLists.txt` e adicione o seguinte comando ao final:

```cmake
# Linking libraries
target_link_libraries(oriented_graph
    PRIVATE
        graph
)
```

## Compilando o projeto

Agora que definimos todas as configura√ß√µes do projeto, podemos compilar o c√≥digo com o CMake.

Internamente, o CMake ir√° gerar uma s√©rie arquivos auxiliares que definem as instru√ß√µes para compilar o projeto.

Esses arquivos s√£o espec√≠ficos para cada ambiente de desenvolvimento e cont√™m instru√ß√µes para compilar, linkar e executar o c√≥digo diferentemente, de acordo com o sistema operacional e compilador utilizados, por exemplo.

Na verdade, o prop√≥sito principal do CMake √© justamente generalizar o processo de compila√ß√£o, gerando arquivos de configura√ß√£o espec√≠ficos para diferentes ambientes.

Dessa forma, o mesmo projeto pode ser compilado em diferentes sistemas operacionais e compiladores sem a necessidade de alterar o c√≥digo fonte.

Esses arquivos auxiliares s√£o muitos e podem poluir o diret√≥rio do projeto.
Para que eles n√£o nos atrapalhem, podemos criar uma pasta chamada `build` e configurar o CMake para gerar os arquivos de compila√ß√£o l√°.

Assim, se ocorrer algum erro durante a compila√ß√£o, √© poss√≠vel simplesmente deletar a pasta `build` e gerar os arquivos novamente!

### Configura√ß√£o de compila√ß√£o

Vamos utilizar dois comandos para compilar o projeto.
O primeiro √© respons√°vel por ler os arquivos `CMakeLists.txt` que escrevemos, criar a pasta `build` e gerar os arquivos de configura√ß√£o necess√°rios dentro dela.

Ele tem a seguinte estrutura:

- `cmake`: comando para executar o CMake.
- `-S .`: indica que o arquivo `CMakeLists.txt` principal est√° na pasta atual (representada pelo `.`)
- `-B build`: indica que os arquivos de configura√ß√£o devem ser gerados na pasta `build`.

Portanto, execute na raiz do projeto `oriented_graph`:

```bash
cmake -S . -B build
```

Se voc√™ tiver instalado o `Ninja`, voc√™ ver√° a seguinte sa√≠da:

![Comando executado para configurar a compila√ß√£o do projeto com o CMake.](configuring_project_with_cmake.png)

Se observar a pasta `build`, ver√° que ela foi criada e cont√©m os arquivos de configura√ß√£o gerados pelo CMake.

### Compila√ß√£o

O segundo comando √© respons√°vel por de fato compilar o projeto.
Ele tem a seguinte estrutura:

- `cmake`: comando para executar o CMake.
- `--build`: indica que o CMake deve compilar o projeto.
- `build`: indica a pasta em que os arquivos de configura√ß√£o foram gerados.

Portanto, execute na raiz do projeto `oriented_graph`:

```bash
cmake --build build
```

Se tudo correr bem, voc√™ n√£o ver√° nenhum erro, e a √∫ltima linha de sa√≠da ser√°:

```bash
[16/16] Linking CXX executable oriented_graph.exe
```

Voc√™ ver√° que um arquivo `oriented_graph.exe` foi gerado na pasta `build`.
Podemos nos mover para essa pasta e executar como anteriormente, ou executar diretamente da raiz do projeto com o comando:

```bash
./build/oriented_graph
```

O `.` indica o diret√≥rio atual, o `build` √© o diret√≥rio onde o execut√°vel foi gerado e `oriented_graph` √© o nome do execut√°vel.

Como anteriormente, voc√™ ver√° a seguinte sa√≠da: `1 3 4 5 2`.

Um detalhe √© que, caso voc√™ n√£o mude a estrutura de depend√™ncias do projeto, n√£o √© necess√°rio rodar o comando `cmake -S . -B build` novamente.
Apenas o comando `cmake --build build` √© suficiente para compilar o projeto.

Caso venha a alterar as depend√™ncias, delete a pasta `build` e rode o comando `cmake -S . -B build` novamente.

### Ignorando arquivos gerados

Os arquivos gerados pelo CMake n√£o s√£o necess√°rios para outras pessoas que vierem a compilar o projeto em outros computadores.

Eles dever√£o utilizar os comandos explicados para gerar seus pr√≥prios arquivos de compila√ß√£o.

Al√©m disso, o arquivo execut√°vel pode acabar tendo um tamanho muito grande, e n√£o √© interessante version√°-lo no reposit√≥rio com o Git.

Por isso, podemos criar um arquivo chamado `.gitignore` na raiz do projeto e adicionar as seguintes linhas:

```gitignore
/build
*.exe
```

O primeiro comando ignora a pasta `build` e todos os arquivos gerados pelo CMake.
O segundo ignora todos os arquivos com a extens√£o `.exe`, que s√£o execut√°veis no Windows.

Dessa forma, apenas os arquivos de c√≥digo fonte e os `CMakeLists.txt` ser√£o inclu√≠dos no controle de vers√£o do Git.

---

> ‚¨ÖÔ∏è [**Cap√≠tulo anterior**](/chapters/compiling_cpp_project/text.md)\
> üè† [**P√°gina inicial**](/README.md)
